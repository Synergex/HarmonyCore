import System
import System.Collections.Generic
import System.Text
import Harmony.Core.Context
import System.Threading.Tasks
import Microsoft.AspNetCore.Builder
import Microsoft.AspNetCore.Http
import Microsoft.Extensions.DependencyInjection

namespace Harmony.AspNetCore.Context

	;;this interface gets used if the context needs to be in the middleware pipeline rather than just dependency injected into the controller
	public interface IContextMiddleware
		method InvokeMiddlware, @Task
			context, @HttpContext
			next, @Func<Task>
		endmethod
	endinterface


	;;allow the user to specify routes where this context should be applied
	;;maybe insert it at the service provider level
	;;if the context is thread isolated we need to transition threads in the middlware
	;;if the context is free threaded we just need to grab one from a pool
	;;if the context is thread safe its really just a singleton grab the one
	;;if the context is process isolated grab one from the pool
	;;if the context is sticky, run the request through the context pool to allow it to either bind or make a new context
	public static class ContextBaseMiddlewareHelper

		class ContextFactoryServiceProvider<T(IContextBase, class)> implements IServiceProvider, IDisposable
			public InnerProvider, @IServiceProvider
			public Context, @HttpContext
			public Factory, @ContextFactory<T>
			public SessionId, @string
			public Value, T
			public virtual method GetService, @object
				serviceType, @Type 
				endparams
			proc
				if(serviceType == ^typeof(T)) then
				begin
					if(Value == ^null)
						Value = Factory.MakeContext()
					mreturn Value
				end
				else
					mreturn InnerProvider.GetService(serviceType)
			endmethod
			
			public virtual method Dispose, void
				endparams
			proc
				if(InnerProvider .is. IDisposable)
					((@IDisposable)InnerProvider).Dispose()
				if(Value .is. IDisposable)
					((@IDisposable)Value).Dispose()
			endmethod
		endclass


		class ContextFactoryHoist<T(IContextBase, class)>
			public Factory, @ContextFactory<T>
			public IsExpensive, boolean
			public IsSticky, boolean
			public async method InvokeMiddlware, @Task
				context, @HttpContext
				next, @Func<Task>
			proc
				data contextFactoryProvider, @ContextFactoryServiceProvider<T>
				data sessionId = IsSticky ? context.Session.GetString(^typeof(T).Name + "FactorySession") : ""
				if(IsSticky && String.IsNullOrEmpty(sessionId))
				begin
					sessionId = Guid.NewGuid().ToString()
					context.Session.SetString(^typeof(T).Name + "FactorySession", sessionId)
				end

				if(IsExpensive) then
				begin
					data madeContext = await Factory.MakeContextAsync(sessionId)
					context.RequestServices = contextFactoryProvider = new ContextFactoryServiceProvider<T>() { InnerProvider = context.RequestServices, Context = context, Factory = Factory, SessionId = sessionId }
					contextFactoryProvider.Value = madeContext
				end
				else
				begin
					context.RequestServices = contextFactoryProvider = new ContextFactoryServiceProvider<T>() { InnerProvider = context.RequestServices, Context = context, Factory = Factory, SessionId = sessionId }
				end
				try
				begin
					await next()
				end
				finally
				begin
					if(contextFactoryProvider.Context != ^null)
					begin
						Factory.ReturnContext(contextFactoryProvider.Value)
					end
				end
				endtry
			endmethod
		endclass

		public static extension method UseContext<T(IContextBase, class)>, @IApplicationBuilder 
			builder, @IApplicationBuilder
			factory, @ContextFactory<T>
		proc
			data hoist = new ContextFactoryHoist<T>() { IsExpensive = false, IsSticky = factory.IsSessionSticky }
			builder.Use(hoist.InvokeMiddlware)
			mreturn builder
		endmethod

		;;this creates the context async inside the middleware process instead of blocking DI
		public static extension method UseExpensiveContext<T(IContextBase, class)>, @IApplicationBuilder 
			builder, @IApplicationBuilder
			factory, @ContextFactory<T>
		proc
			data hoist = new ContextFactoryHoist<T>() { IsExpensive = true, IsSticky = factory.IsSessionSticky}
			builder.Use(hoist.InvokeMiddlware)
			mreturn builder
		endmethod
	endclass

endnamespace
