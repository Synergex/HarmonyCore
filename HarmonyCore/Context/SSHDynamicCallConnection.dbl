import System
import System.Collections.Generic
import System.Text
import Renci.SshNet
import Renci.SshNet.Common
import System.IO
import Newtonsoft.Json
import System.Diagnostics
import System.Threading.Tasks
import StreamJsonRpc
import System.Threading
import Harmony.Core.Utility

namespace Harmony.Core.Context

	public class SSHDynamicCallConnection implements IDynamicCallConnection
		private static NewLineBytes, [#]Byte, new byte[#] { 10, 13 }
		private mTargetConnection, @SshClient
		private mTargetShell, @ShellStream
		private mReader, @StreamReader
		private mWriter, @StreamWriter
		private mLaunchCommand, @string
		private mMakeReadyTask, @Lazy<Task<JsonRpc>>
		private mTargetOS, RemoteTargetOS
		private mNeedsRecycle, boolean, false
		private mCallTimeout, int
		private mConnectionTimeout, int
		public method SSHDynamicCallConnection
			targetOS, RemoteTargetOS
			startInfo, @ConnectionInfo
			command, @string
			callTimeout, int
			connectionTimeout, int
		proc
			mCallTimeout = callTimeout
			mConnectionTimeout = connectionTimeout
			mTargetOS = targetOS
			mLaunchCommand = command
			mTargetConnection = new SshClient(startInfo)
			mMakeReadyTask = new Lazy<Task<JsonRpc>>(MakeReadyInternal)
		endmethod
		
		public virtual async method Recycle, @Task
			endparams
		proc
			if(mNeedsRecycle && mMakeReadyTask.IsValueCreated)
			begin
				data jsonRpcConnection, @JsonRPC
				try
				begin
					mNeedsRecycle = false
					jsonRpcConnection = await mMakeReadyTask.Value
					data cancelSource = new CancellationTokenSource(mCallTimeout)
					data readOnlyArgs = ^as(new object[0], @IReadOnlyList<Object>)
					await jsonRpcConnection.InvokeWithCancellationAsync<int>("rpc.ping", readOnlyArgs, cancelSource.Token)
				end
				catch(ex, @Exception)
				begin
					if(jsonRpcConnection != ^null && !jsonRpcConnection.IsDisposed)
					begin
						jsonRpcConnection.Dispose()
					end
					Disconnect()
					throw
				end
				endtry
			end
		endmethod
				
		public virtual async method Disconnect, @Task
			endparams
		proc
			try
			begin
				if(mTargetConnection.IsConnected)
				begin
					await this.Notify("rpc.shutdown", new ArgumentDataDefinition[0])
					mReader.Close()
					mTargetConnection.Disconnect()
				end
			end
			catch(ex, @Exception)
			begin
				DebugLogSession.Logging.LogException(ex, "SSHDynamicCallConnection-Disconnect")
			end
			endtry

		endmethod

		public virtual async method Call, @Task<[#]@ReturnParameterDefintion>
			name, @string
			args, [#]@ArgumentDataDefinition
		proc
			data jsonRpcConnection = await mMakeReadyTask.Value
			try
			begin
				data cancelSource = new CancellationTokenSource(mCallTimeout)
				data readOnlyArgs = ^as(args, @IReadOnlyList<ArgumentDataDefinition>)
				data result = await jsonRpcConnection.InvokeWithCancellationAsync<[#]@ReturnParameterDefintion>(name, readOnlyArgs, cancelSource.Token)
				mreturn result
			end
			catch(ex, @RemoteInvocationException)
			begin
				mNeedsRecycle = true
				throw new RemoteInvocationException(Encoding.UTF8.GetString(Convert.FromBase64String(ex.Message)), ex.ErrorCode, ex.Data)
			end
			catch(ex, @Exception)
			begin
				mNeedsRecycle = true
				throw
			end
			endtry

		endmethod

		public virtual async method Notify, @Task
			name, @string
			args, [#]@ArgumentDataDefinition
		proc
			data jsonRpcConnection = await mMakeReadyTask.Value
			try
			begin
				data cancelSource = new CancellationTokenSource(mCallTimeout)
				data readOnlyArgs = ^as(args, @IReadOnlyList<ArgumentDataDefinition>)
				await jsonRpcConnection.NotifyAsync(name, readOnlyArgs)
			end
			catch(ex, @RemoteInvocationException)
			begin
				mNeedsRecycle = true
				throw new RemoteInvocationException(Encoding.UTF8.GetString(Convert.FromBase64String(ex.Message)), ex.ErrorCode, ex.Data)
			end
			catch(ex, @Exception)
			begin
				mNeedsRecycle = true
				throw
			end
			endtry

		endmethod

		public async virtual method MakeReadyInternal, @Task<JsonRpc>
			endparams
		proc
			data cancelSource = new CancellationTokenSource(mConnectionTimeout)
			data cancelToken = cancelSource.Token
			mTargetConnection.Connect()
			mTargetShell = mTargetConnection.CreateShellStream("vt220", 0, 0,0,0, 1024 * 64)
			data streamWrapper = new ShellStreamWrapper(mTargetShell, mTargetConnection)
			mReader = new StreamReader(streamWrapper) 
			mWriter = new StreamWriter(streamWrapper) 
			data guidString, @string, Guid.NewGuid().ToString()
			data guidStrings = new HashSet<string>()
			data readLine, @string
			data foundMatch, boolean, false
			guidStrings.Add(guidString)

			while(String.IsNullOrWhiteSpace(readLine) || readLine != guidString)
			begin
				cancelToken.ThrowIfCancellationRequested()
				if(readLine == ^null || !foundMatch)
				begin
					if(mTargetOS == RemoteTargetOS.VMS) then
					begin
						await mWriter.WriteAsync('WRITE SYS$OUTPUT "' + guidString +  '"' + %char(13) + %char(10))
					end
					else
					begin
						await mWriter.WriteAsync('echo "' + guidString +  '"' + %char(13) + %char(10))
					end

					await mWriter.FlushAsync()
				end

				readLine = TrimControlChars(await mReader.ReadLineAsync())
				while(String.IsNullOrWhiteSpace(readLine))
				begin
					cancelToken.ThrowIfCancellationRequested()
					readLine = TrimControlChars(await mReader.ReadLineAsync())
				end
				
				data checkString, @string
				foundMatch = false
				foreach checkString in guidStrings
				begin
					if(readLine.Contains(checkString)) 
					begin
						foundMatch = true
						exitloop
					end
				end

				if(!foundMatch)
				begin
					guidString = Guid.NewGuid().ToString()
					guidStrings.Add(guidString)
				end
			end

			
			await mWriter.WriteAsync(mLaunchCommand + %char(13) + %char(10))
			await mWriter.FlushAsync()

			do
			begin
				cancelToken.ThrowIfCancellationRequested()
				readLine = TrimControlChars(await mReader.ReadLineAsync())
			end
			until(readLine == "READY")
			
			
			data result = new JsonRPC(new HeaderDelimitedMessageHandler(streamWrapper, new JsonMessageFormatter(Encoding.ASCII)));;new DynamicCallProvider.HarmonyJsonMessageFormatter(Encoding.Ascii))) 
			result.TraceSource = new TraceSource("Client", SourceLevels.All)
			result.StartListening()
			mreturn result
		endmethod
	
		public method TrimControlChars, @string
			arg, @string
		proc
			if(arg == ^null)
				mreturn ^null

			data arrForm = arg.ToCharArray()
			data buffer = new StringBuilder(arg.Length)
			data ch, char
			foreach ch in arrForm
				if (!Char.IsControl(ch) && !Char.IsWhiteSpace(ch)) 
					buffer.Append(ch)
 
			mreturn buffer.ToString()
		endmethod



		public virtual method MakeReady, @Task
		proc
			mreturn mMakeReadyTask.Value
		endmethod

		private class ShellStreamWrapper extends Stream

			public override method Read, int
				buffer, [#]byte 
				offset, int 
				count, int 
				endparams
			proc
				data readBytes = mStream.Read(buffer, offset, count)
				DebugLogSession.Logging.LogTrace("SSHDynamicCallConnection id:{0} read ascii string: {1}", mConnectionId, new ASCIIArrayDebugLogHelper(buffer, offset, readBytes))
				mreturn readBytes

			endmethod

			private static mConnectionId, int, 0
			private mStream, @ShellStream
			private mConnection, @SshClient
			;private mEvent, @AutoResetEvent
			public method ShellStreamWrapper
				stream, @ShellStream
				connection, @SshClient
			proc
				mStream = stream
				mConnection = connection
				Interlocked.Increment(mConnectionId)
				DebugLogSession.Logging.LogTrace("Created SSHDynamicCallConnection id:{0} : {1}", mConnectionId, new SSHConnectionDebugLogHelper(mConnection.ConnectionInfo))
				;mEvent = new AutoResetEvent(false)
			endmethod


			public override property CanRead, Boolean
				method get
				proc
					mreturn mStream.CanRead
				endmethod
			endproperty

			public override method Close, void
			proc
				DebugLogSession.Logging.LogTrace("SSHDynamicCallConnection id:{0} closed", mConnectionId)
			endmethod

			public override method Flush, void
				endparams
			proc
				mStream.Flush()
			endmethod

			public override method ReadAsync, @Task<int> 
				buffer, [#]byte 
				offset, int 
				count, int 
				token, CancellationToken
				endparams
			proc
				
				if(mStream.DataAvailable) then
				begin
					data readBytes = mStream.Read(buffer, offset, count)
					DebugLogSession.Logging.LogTrace("SSHDynamicCallConnection id:{0} read ascii string: {1}", mConnectionId, new ASCIIArrayDebugLogHelper(buffer, offset, readBytes))
					mreturn Task.FromResult(readBytes)
				end
				else
				begin
					data tcs = new TaskCompletionSource<int>()
					data handlerInstance, @EventHandler<ShellDataEventArgs>
					lambda DataReceivedHandler(sender, args)
					begin
						mStream.DataReceived -= handlerInstance
						data readBytes = mStream.Read(buffer, offset, count)
						DebugLogSession.Logging.LogTrace("SSHDynamicCallConnection id:{0} read ascii string: {1}", mConnectionId, new ASCIIArrayDebugLogHelper(buffer, offset, readBytes))
						tcs.TrySetResult(readBytes)
					end

					data errorHandlerInstance, @EventHandler<ExceptionEventArgs>
					lambda ErrorHandler(sender, args)
					begin
						mStream.ErrorOccurred -= errorHandlerInstance
						tcs.TrySetException(args.Exception)
					end
					handlerInstance = DataReceivedHandler
					errorHandlerInstance = ErrorHandler
					mStream.DataReceived += handlerInstance
					mConnection.ErrorOccurred += errorHandlerInstance
					mreturn tcs.Task
				end
			endmethod

			public override property CanTimeout, boolean
				method get
				proc
					mreturn true
				endmethod
			endproperty


			public override property CanSeek, Boolean
				method get
				proc
					mreturn mStream.CanSeek
				endmethod
			endproperty



			public override property Position, long
				method get
				proc
					mreturn mStream.Position
				endmethod
				method set
				proc
					mStream.Position = value
				endmethod
			endproperty



			public override property Length, long
				method get
				proc
					mreturn mStream.Length
				endmethod
			endproperty



			public override method Seek, long
				offset, long 
				origin, SeekOrigin 
				endparams
			proc
				mreturn mStream.Seek(offset, origin)
			endmethod



			public override property CanWrite, Boolean
				method get
				proc
					mreturn mStream.CanWrite
				endmethod
			endproperty



			public override method Write, void
				buffer, [#]byte 
				offset, int 
				count, int 
				endparams
			proc
				DebugLogSession.Logging.LogTrace("SSHDynamicCallConnection id:{0} write ascii string: {1}", mConnectionId, new ASCIIArrayDebugLogHelper(buffer, offset, count))
				mStream.Write(buffer, offset, count)
				mStream.Flush()
			endmethod



			public override method SetLength, void
				value, long 
				endparams
			proc
				mStream.SetLength(value)
			endmethod


		endclass

	endclass

endnamespace
