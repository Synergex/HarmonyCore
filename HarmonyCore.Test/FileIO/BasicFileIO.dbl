import System
import Microsoft.VisualStudio.TestTools.UnitTesting
import Harmony.Core
import Harmony.Core.FileIO
import HarmonyCore.Test
import Harmony.Core.Test.FileIO.Models

namespace Harmony.Core.Test.FileIO
	{TestClass}
	public class BasicFileIO

		{TestMethod}
		public method ReadInput, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.InputIndexed)
			data firstOrder, @Orders
			data lastOrder, @Orders
			data nextOrder, @Orders
			data shouldBeFirstOrder, @Orders
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadFirstRecord(firstOrder))
			firstOrder.InternalSynergyRecord(lambda(recordData, grfaData) { Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.FindRecord(-1, grfaData)) })
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.FindFirstRecord())
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadNextRecord(shouldBeFirstOrder))
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadNextRecord(nextOrder))
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadLastRecord(lastOrder))


			Assert.AreEqual(firstOrder.OrderNumber, shouldBeFirstOrder.OrderNumber)
			Assert.IsTrue(firstOrder.OrderNumber < nextOrder.OrderNumber)
			Assert.IsTrue(nextOrder.OrderNumber < lastOrder.OrderNumber)
		endmethod

		{TestMethod}
		public method WriteOrders, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.UpdateIndexed)
			data firstOrder, @Orders
			data firstOrderClone, @Orders
			data lastOrder, @Orders
			
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadFirstRecord(firstOrder))
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadLastRecord(lastOrder))

			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.ReadFirstRecord(firstOrderClone))
			firstOrder.CustomerReference = "ZZZ"
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.Success, fileIO.UpdateRecordUsingGRFA(firstOrder))
			Assert.AreEqual(Harmony.Core.FileIO.FileAccessResults.RecordDataNotSame, fileIO.UpdateRecordUsingGRFA(firstOrderClone))
		endmethod

		{TestMethod}
		public method CreateOrder, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.UpdateIndexed)
			data newOrder, @Orders

			Assert.AreEqual(FileAccessResults.Success, fileIO.CreateRecord(new Orders() { OrderNumber = 987654 } ))
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadLastRecord(newOrder))
			Assert.AreEqual(987654, newOrder.OrderNumber)
		endmethod

		{TestMethod}
		public method DeleteOrder, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.UpdateIndexed)
			data lastOrder, @Orders

			;; Delete record
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadLastRecord(lastOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.ManualLockUsingGRFA(lastOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.DeleteRecord())

			;; Delete record with GRFA
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadLastRecord(lastOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.ManualLockUsingGRFA(lastOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.DeleteRecordUsingGRFA(lastOrder))

			;; Delete record with GRFA and lock
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadLastRecord(lastOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.ManualLockUsingGRFA(lastOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.DeleteRecordUsingGRFA(lastOrder, true))
		endmethod

		{TestMethod}
		public method FileIOKey, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.UpdateIndexed)
			data lastOrder, @Orders
			
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadLastRecord(lastOrder))
			Assert.AreEqual("000027", fileIO.GetKeyValue(lastOrder, 1))
			Assert.AreEqual(6, fileIO.KeyLength(1))
			Assert.AreEqual(7, fileIO.KeyPosition(1))
		endmethod

		{TestMethod}
		public method GetProperties, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.UpdateIndexed)
			
			Assert.AreEqual(4, fileIO.NumberOfKeys)
			Assert.AreEqual(4722, fileIO.NumberOfRecords)
			Assert.AreEqual("DAT:orders.ism", fileIO.OpenFileName)
			Assert.AreEqual(100, fileIO.SizeOfRecord)
		endmethod

		{TestMethod}
		public method Errors, void
		proc
			disposable data channelManager = new FileChannelManager()
			disposable data fileIO = new IsamDataObjectIO<Orders>(channelManager, "DAT:orders.ism", FileOpenMode.UpdateIndexed)
			data firstOrder, @Orders

			;; Lock and delete
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadFirstRecord(firstOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.UnlockChannel())
			Assert.AreEqual(FileAccessResults.NoCurrentRecordLocked, fileIO.DeleteRecord())
			Assert.AreEqual(FileAccessResults.NoCurrentRecordLocked, fileIO.DeleteRecordUsingGRFA(firstOrder))
			Assert.AreEqual(FileAccessResults.UnknownError, fileIO.ManualLockUsingGRFA(new Orders()))

			;; Delete twice
			Assert.AreEqual(FileAccessResults.Success, fileIO.ManualLockUsingGRFA(firstOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.DeleteRecordUsingGRFA(firstOrder))
			Assert.AreEqual(FileAccessResults.UnknownError, fileIO.DeleteRecordUsingGRFA(firstOrder))

			;; Bad keys
			Assert.AreEqual(0, fileIO.KeyLength(95670))
			Assert.AreEqual(0, fileIO.KeyPosition(95670))

			;; Bad finds
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadFirstRecord(firstOrder))
			Assert.AreEqual(FileAccessResults.EndOFDataFile, fileIO.FindRecord("keyValue"))
			Assert.AreEqual(FileAccessResults.EndOFDataFile, fileIO.FindRecord(95670))
			Assert.AreEqual(FileAccessResults.EndOFDataFile, fileIO.FindRecord(1, "keyValue"))
			Assert.AreEqual(FileAccessResults.UnknownError, fileIO.FindRecord(95670, "keyValue"))
			Assert.AreEqual(FileAccessResults.DuplicateKeyEncountered, fileIO.CreateRecord(firstOrder))
			Assert.AreEqual(string.Empty, fileIO.GetKeyValue(firstOrder, 95670))

			;; Bad update
			Assert.AreEqual(FileAccessResults.NoCurrentRecordLocked, fileIO.UpdateRecord(new Orders()))
			Assert.AreEqual(FileAccessResults.Success, fileIO.ReadFirstRecord(firstOrder))
			Assert.AreEqual(FileAccessResults.Success, fileIO.ManualLockUsingGRFA(firstOrder))
			Assert.AreEqual(FileAccessResults.KeyNotSame, fileIO.UpdateRecord(new Orders()))
		endmethod

	endclass
endnamespace
